apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'jacoco'

group = 'com.commercetools'
version = '1.0-SNAPSHOT'

description = """"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

ext {
    commercetoolsJvmSdkVersion =  '1.12.0'
    mockitoVersion = '1.10.19'
    slf4jVersion = '1.6.2'
    jUnitVersion = '4.12'
    assertjVersion = '3.6.1'
    checkstyleVersion = '7.7'
    pmdVersion = '5.6.1'
    jacocoVersion = '0.7.9'
}

repositories {
    maven { url "http://repo.maven.apache.org/maven2" }
}
dependencies {
    compile group: 'com.commercetools.sdk.jvm.core', name: 'commercetools-models', version: commercetoolsJvmSdkVersion
    compile group: 'com.commercetools.sdk.jvm.core', name: 'commercetools-java-client', version: commercetoolsJvmSdkVersion
    compile group: 'com.commercetools.sdk.jvm.core', name: 'commercetools-convenience', version: commercetoolsJvmSdkVersion
    compile group: 'org.mockito', name: 'mockito-all', version: mockitoVersion
    runtime group: 'org.slf4j', name: 'slf4j-simple', version: slf4jVersion
    testCompile group: 'junit', name: 'junit', version: jUnitVersion
    testCompile group: 'org.assertj', name: 'assertj-core', version: assertjVersion
}

checkstyle {
    toolVersion = checkstyleVersion
    maxWarnings = 0
}
pmd {
    toolVersion = pmdVersion
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/pmd.xml')
    rulePriority = 2 // If a rule priority, with a level greater than or equal this number, is violated; the build should fail.
}

jacoco {
    toolVersion = jacocoVersion
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.8 // test coverage should be at a minimum of 80%, otherwise build would fail.
            }
        }
    }
}

jacocoTestReport {
    reports {
        html.enabled = true
    }
}
check.dependsOn jacocoTestCoverageVerification // Build should fail if jacocoTestCoverageVerification task rules are not fulfilled.
test.finalizedBy jacocoTestReport